# Run `just` to see available recipes

APP_NAME       := "realworld-liberty"
IMAGE_NAME   := APP_NAME + ":latest"
SERVER_PORT    := "8080"               # Application server internal HTTP
HOST_PORT      := "10012"               # Host port to map to Application server's internal HTTP
POSTGRES_CONTAINER_NAME  := "postgres-liberty"
NETWORK_NAME   := "realworld-liberty-net"

# Set shell
set shell := ["bash", "-c"]

# Default recipe - shows available commands
default:
    @just --list

# Build the docker image
build:
    docker build -f Dockerfile -t {{IMAGE_NAME}} .
    @echo "[INFO] Built image: {{IMAGE_NAME}}"

# Rebuild the docker image, without cache
rebuild:
    docker build --no-cache -f Dockerfile -t {{IMAGE_NAME}} .
    @echo "[INFO] Rebuilt image (no cache): {{IMAGE_NAME}}"

# Create docker network
network:
    @if docker network ls --quiet --filter name=^/{{NETWORK_NAME}}$ | grep -q .; then \
        echo "[INFO] Removing existing network"; \
        docker network rm -f {{NETWORK_NAME}} >/dev/null; \
    fi
    docker network create {{NETWORK_NAME}}

# Start PostgreSQL container
db-up: network
    @if docker ps --all --quiet --filter name=^/{{POSTGRES_CONTAINER_NAME}}$ | grep -q .; then \
        echo "[INFO] Removing existing PostgreSQL container"; \
        docker rm -f {{POSTGRES_CONTAINER_NAME}} >/dev/null; \
    fi
    @echo "Starting PostgreSQL 17 container..."    
    docker run -d \
        --name {{POSTGRES_CONTAINER_NAME}} \
        --network {{NETWORK_NAME}} \
        -e POSTGRES_PASSWORD=S3cret \
        -e POSTGRES_USER=postgres_user \
        -e POSTGRES_DB=postgres_db \
        -p 5432:5432 \
        postgres:17
    @echo "Waiting for PostgreSQL to start..."
    @until docker exec {{POSTGRES_CONTAINER_NAME}} pg_isready > /dev/null 2>&1; do sleep 1; done
    @echo "PostgreSQL is ready."

# Run the app in a docker container
up: build db-up
    @if docker ps --all --quiet --filter name=^/{{APP_NAME}}$ | grep -q .; then \
        echo "[INFO] Removing existing container"; \
        docker rm -f {{APP_NAME}} >/dev/null; \
    fi
    docker run -d \
        --name {{APP_NAME}} \
        --network {{NETWORK_NAME}} \
        -p {{HOST_PORT}}:{{SERVER_PORT}} \
        -e POSTGRESQL_HOSTNAME={{POSTGRES_CONTAINER_NAME}} \
        {{IMAGE_NAME}}
    @echo "[INFO] Started {{APP_NAME}}, waiting for app to start..."
    @until docker logs {{APP_NAME}} 2>&1 | grep -q "CWWKF0011I"; do sleep 1; done
    @echo "[INFO] Application server started, port {{HOST_PORT}} is ready."

# See the app logs
logs:
    docker logs -f {{APP_NAME}}

# Stop the app
down:
    docker rm -f {{APP_NAME}}
    docker rm -f {{POSTGRES_CONTAINER_NAME}}
    @echo "[INFO] Container removed (if it existed)"

# Clean up
clean: down
    docker rmi {{APP_NAME}}
    docker rmi postgres:17
    docker network rm {{NETWORK_NAME}}
    @echo "[INFO] Image removed (if it existed)"

# Run the smoke tests
test: up
    @docker exec {{APP_NAME}} sh -c 'cd smoke && uv sync && uv run smoke.py'

# Run the app locally
local: db-up
    mvn clean package liberty:run
